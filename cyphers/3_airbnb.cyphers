// ----------------------------------------------
// insert Airbnb listings
// ----------------------------------------------
CALL apoc.load.json('FILE:///airbnb_listings.json')
YIELD value
WHERE value.room_type_category = 'entire_home'
MERGE (r:Rental {id: value.id})
MERGE (c:City {name: value.city})
MERGE (r)-[:Located_In]->(c)
SET r.type_id = value.property_type_id,
    r.tier_id = value.tier_id,
    r.description = value.name,
    r.bed = value.bedrooms,
    r.bath = value.bathrooms,
    r.bedroom_label = value.bedroom_label,
    r.bathroom_label = value.bathroom_label,
    r.coordinate = point(value.coordinate),
    r.space_type = value.space_type,
    r.amenity_names = value.preview_amenity_names,
    r.amenity_ids = value.amenity_ids,
    r.price = toInteger(value.rate)
RETURN
    count(r) AS Airbnb_Rental,
    count(distinct c) AS Airbnb_City;

// ----------------------------------------------
// connect Airbnb listings to neighborhoods
// ----------------------------------------------
CALL apoc.load.json('FILE:///airbnb_listings.json')
YIELD value
MATCH (r:Rental), (c:City)
WHERE r.id = value.id AND c.name = value.city
UNWIND value.neighborhood AS neighborhood
MERGE (n:Neighborhood {name: neighborhood})
MERGE (r)-[:Located_In]->(n)
MERGE (n)-[:Located_In]->(c)
RETURN
    count(distinct n) AS Airbnb_Neighborhood,
    count(n) AS Airbnb_Neighborhood_Relationship;

// ----------------------------------------------
// insert Airbnb reviews and connect to listings
// ----------------------------------------------
CALL apoc.load.json('FILE:///airbnb_reviews.json')
YIELD value
MATCH (a:Rental)
WHERE a.id = value.listing_id
MERGE (b:Review {id: value.id})
MERGE (a)-[r:Is_Reviewed]->(b)
SET b.listing_id = value.listing_id,
    b.author_id = value.author_id,
    b.recipient_id = value.recipient_id,
    b.rating = value.rating,
    b.text = value.comments,
    b.response = value.response,
    b.time_created = datetime(value.created_at)
RETURN count(b) AS Airbnb_Review;

// ----------------------------------------------
// insert Airbnb review topics
// ----------------------------------------------
CALL apoc.load.json('FILE:///airbnb_review_topics.json')
YIELD value
MERGE (t:Topic:Rental {id: value.topic})
SET t.words = value.words
WITH value, t
UNWIND value.neighborhood AS neighborhood
MATCH (n:Neighborhood)
WHERE n.name = neighborhood
MERGE (n)-[r:Has_Topic]->(t)
RETURN
    count(distinct t) AS Airbnb_Topic,
    count(r) AS Airbnb_Topic_Relationship;

// ----------------------------------------------
